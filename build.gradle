apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'java'
apply plugin: 'application'

version = '1.0.0'
mainClassName = 'masoes.app.Main'

def gitRevision = 'git rev-parse HEAD'.execute().text.trim()
def appName = rootProject.name
def argsProperty = 'args'

repositories {
    mavenCentral()
    maven {
        url 'http://jade.tilab.com/maven/'
    }
}

dependencies {
    compile 'org.apache.logging.log4j:log4j-api:2.6.2'
    compile 'org.apache.logging.log4j:log4j-core:2.6.2'
    compile 'org.apache.logging.log4j:log4j-slf4j-impl:2.6.2'
    compile 'org.slf4j:slf4j-api:1.7.21'
    compile 'commons-cli:commons-cli:1.3.1'
    compile 'com.tilab.jade:jade:4.4.0'
    compile 'com.vividsolutions:jts:1.13'

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'org.unitils:unitils-core:3.4.2'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile 'com.github.stefanbirkner:system-rules:1.16.0'
}

processResources {
    expand('appName': appName, 'appVersion': version, 'appRevision': gitRevision)
}

jar {
    manifest {
        attributes('Implementation-Title': appName, 'Implementation-Version': version, 'Implementation-Revision': gitRevision)
    }
}

run {
    main = mainClassName
    if (project.hasProperty(argsProperty)) {
        args(project.property(argsProperty).split())
    }
}

test {
    testLogging {
        showExceptions true
        showCauses true
        showStackTraces true
        exceptionFormat 'full'
        events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
    }
}
